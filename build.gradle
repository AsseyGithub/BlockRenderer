import java.time.Instant

buildscript {
	repositories {
		maven { url = 'https://maven.fabricmc.net/' }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'fabric-loom', name: 'fabric-loom.gradle.plugin', version: '0.2.+', changing: true
	}
}

apply plugin: 'fabric-loom'

group = "com.unascribed"
archivesBaseName = "BlockRenderer"
version = "1.16.2-1.2.0-fabric"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
	// https://modmuss50.me/fabric.html
	minecraft 'com.mojang:minecraft:1.16.2'
	mappings 'net.fabricmc:yarn:1.16.2+build.14:v2'
	modImplementation 'net.fabricmc:fabric-loader:0.9.1+build.205'

	// Fabric api
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.18.0+build.397-1.16"

	// Javax Annotations
	modImplementation 'com.google.code.findbugs:jsr305:3+'
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

jar {
	manifest {
		attributes([
				'Specification-Title'     : rootProject.name,
				'Specification-Vendor'    : project.group,
				'Specification-Version'   : '1',
				'Implementation-Title'    : rootProject.name,
				'Implementation-Version'  : project.version,
				'Implementation-Vendor'   : project.group,
				'Implementation-Timestamp': Instant.now()
		])
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}
